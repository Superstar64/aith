#/Copyright (C) 2016  Freddy Angel Cubas "Superstar64"
 Boost License
/#

alias qsort=$() -> int_t.4[] {
	let array = $@;
	if array.length > 1 then {
		let pivot = array[array.length - 1];
		let uint_t.0 middle = 0;
		let uint_t.0 end = 0;
		while end < array.length - 1 then {
			#0..middle < pivot
			#middle..end - 1 > pivot
			if pivot > array[end] then {
				let last = array[end];
				let current = array[middle];
				array[middle] = last;
				array[end] = current;
				middle = middle + 1;
			};
			end = end + 1;
		};
		let last = array[middle];
		array[middle] = pivot;
		array[array.length - 1] = last;
		qsort(array[0..middle]);
		qsort(array[middle..array.length]);
	};
	()
};

alias main = $() -> () {
	let array = [cast(int_t.4)(0),7,9,3,8,6,5,1,4,2];
	qsort(array);
	assert(array == [cast(int_t.4)(0),1,2,3,4,5,6,7,8,9]); 
};

alias (struct ())(bool_t) assert = extern;
