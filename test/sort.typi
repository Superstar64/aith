#/Copyright (C) 2016  Freddy Angel Cubas "Superstar64"
 Boost License
/#

void = tuple();

swap = >(left,right) = {
	let tmp = *left;
	left <- *right;
	right <- tmp;
};

quick sort : integer 32[*] -> void = >(array) = {
	if array.length > 1 then {
		let pivot = array[array.length - 1];
		let middle = new 0;
		let end = new 0;
		while *end < array.length - 1 then {
			#0..middle < pivot
			#middle..end - 1 > pivot
			if pivot > array[*end] then {
				swap(array&[*middle], array&[*end]);
				middle <- *middle + 1;
			};
			end <- *end + 1;
		};
		swap(array&[*middle],array&[array.length - 1]);
		quick sort(array[0..*middle]);
		quick sort(array[*middle..array.length]);
	};
};

main : void -> void = >(x) = {
	let array : integer 32 [*] = [0,7,9,3,8,6,5,1,4,2];
	quick sort(array);
	assert(array == [0,1,2,3,4,5,6,7,8,9]); 
};

assert : boolean -> void = extern "assert";
